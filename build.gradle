buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.10.1"
    }
}

group = 'uk.co.automatictester'
version = sauceReportingGradlePluginVersion

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-gradle-plugin'
apply plugin: 'signing'
apply plugin: 'com.gradle.plugin-publish'

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }

    doFirst {
        println "\nThis task takes a while to execute, due to Gradle compatibility tests " +
                "and integration tests with Sauce Labs.\nSubsequent executions will be significantly faster " +
                "but still time consuming.\n"
    }
    systemProperties System.properties
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    useTestNG()
    reports.html.destination = file("${reporting.baseDir}/${name}")
    testLogging {
        events "started", "passed", "skipped", "failed"
    }
}

dependencies {
    compile 'com.saucelabs:saucerest:1.0.40'
    testCompile 'org.testng:testng:6.14.3'
    testCompile 'org.mockito:mockito-core:2.28.2'
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId group
            artifactId 'saucelabs-reporting-gradle-plugin'
            version sauceReportingGradlePluginVersion

            from components.java
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (findProperty('ossrhUsername') == null) {
                ext.ossrhUsername = ''
                ext.ossrhPassword = ''
            }

            println "Using OSSRH credentials: ${ossrhUsername}"

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                group group
                name 'saucelabs-reporting-gradle-plugin'
                version sauceReportingGradlePluginVersion
                packaging 'jar'
                description 'Gradle plugin for reporting test results to Sauce Labs'
                url 'http://automatictester.github.io/saucelabs-reporting-gradle-plugin'

                scm {
                    connection 'scm:git:git@github.com:automatictester/saucelabs-reporting-gradle-plugin.git'
                    developerConnection 'scm:git:git@github.com:automatictester/saucelabs-reporting-gradle-plugin.git'
                    url 'git@github.com:automatictester/saucelabs-reporting-gradle-plugin.git'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        name 'Ireneusz Pastusiak'
                        email 'ireneusz.pastusiak@gmail.com'
                        organizationUrl 'https://github.com/automatictester'
                    }
                }
            }
        }
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

pluginBundle {
    website = 'https://github.com/automatictester/saucelabs-reporting-gradle-plugin'
    vcsUrl = 'https://github.com/automatictester/saucelabs-reporting-gradle-plugin'
    description = 'Report test results to Sauce Labs'
    tags = ['saucelabs']

    plugins {
        SaucelabsReportingPlugin {
            id = 'uk.co.automatictester.saucelabs-reporting'
            displayName = 'Sauce Labs Reporting Gradle Plugin'
        }
    }
}